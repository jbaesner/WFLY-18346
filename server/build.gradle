apply plugin: 'eclipse'
apply plugin: 'java'
apply plugin: 'war'

configurations {
  wildfly
}

dependencies {
  implementation project(':interfaces')
  implementation 'jakarta.platform:jakarta.jakartaee-api:10.0.0'

  wildfly "org.wildfly:wildfly-dist:${versions.wildfly}@zip"
}

File wildFlyDir = new File(buildDir, 'wildfly')

task downloadServer(type: Copy) {
  from configurations.wildfly
  into temporaryDir
}

task installServer(type: Copy) {
  dependsOn downloadServer
  from zipTree(new File(downloadServer.temporaryDir, "wildfly-dist-${versions.wildfly}.zip"))
  into wildFlyDir
}

task configureServer(type: Exec) {
  dependsOn installServer
  workingDir new File(wildFlyDir, "wildfly-${versions.wildfly}/bin")
  if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
    commandLine 'cmd', '/c', "add-user.bat -a -u username -p password"
  } else {
    commandLine 'sh', '-c', "./add-user.sh -a -u username -p password"
  }
}

task startServer {
  dependsOn configureServer
  doLast {
    ProcessBuilder processBuilder
    if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
      processBuilder = new ProcessBuilder('cmd', '/c', 'standalone.bat')
    } else {
      processBuilder = new ProcessBuilder('sh', '-c', './standalone.sh')
    }
    processBuilder.directory(new File(wildFlyDir, "wildfly-${versions.wildfly}/bin"))
    processBuilder.environment().put('JAVA_HOME', org.gradle.internal.jvm.Jvm.current().javaHome.absolutePath)
    File logFile = new File(buildDir, 'logs/std.log')
    logFile.parentFile.mkdirs()
    processBuilder.redirectErrorStream(true).redirectOutput(logFile)
    processBuilder.start()
  }
}

task deployServer(type: Exec) {
  dependsOn war
  workingDir new File(wildFlyDir, "wildfly-${versions.wildfly}/bin")
  if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
    commandLine 'cmd', '/c', "jboss-cli.bat -c \"deploy --force ${war.archiveFile.get()}\""
  } else {
    commandLine 'sh', '-c', "./jboss-cli.sh -c \"deploy --force ${war.archiveFile.get()}\""
  }
}

task stopServer(type: Exec) {
  dependsOn war
  workingDir new File(wildFlyDir, "wildfly-${versions.wildfly}/bin")
  if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
    commandLine 'cmd', '/c', "jboss-cli.bat -c shutdown"
  } else {
    commandLine 'sh', '-c', "./jboss-cli.sh -c \"shutdown\""
  }
}
